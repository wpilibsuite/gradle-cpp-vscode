plugins {
    id 'java-gradle-plugin'
    id 'maven-publish'
    id 'groovy'
    id "com.gradle.plugin-publish" version "1.2.1"
}

group 'edu.wpi.first'
version '2.1.0'

if (project.hasProperty('publishVersion')) {
    version = project.publishVersion
}

repositories {
    maven {
        url "https://plugins.gradle.org/m2/"
    }
    mavenLocal()
}

java {
    sourceCompatibility = 11
    targetCompatibility = 11
}

dependencies {
    api 'com.google.code.gson:gson:2.8.6'

    testImplementation('org.spockframework:spock-core:2.0-M4-groovy-3.0') {
        exclude group: 'org.codehaus.groovy'
    }
    testImplementation("org.junit.jupiter:junit-jupiter-api:5.8.2")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine")
    testImplementation gradleTestKit()
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()
}

gradlePlugin {
    website = 'https://github.com/wpilibsuite/gradle-cpp-vscode'
    vcsUrl = 'https://github.com/wpilibsuite/gradle-cpp-vscode'

    plugins {
        GradleVsCode {
            id = 'edu.wpi.first.GradleVsCode'
            displayName = 'GradleVsCode'
            implementationClass = 'edu.wpi.first.vscode.GradleVsCode'
            description = 'This plugin provides VS Code support for old style gradle projects.'
            tags = ['groovy', 'vscode', 'cpp', 'maven', 'frc', 'wpilib']
        }
    }
}

wrapper {
    gradleVersion = '8.4'
}
